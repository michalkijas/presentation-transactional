buildscript {
    dependencies {
        classpath "com.societegenerale.commons:arch-unit-gradle-plugin:2.6.1"
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext["groovy.version"] = "3.0.5"
ext["junit-jupiter.version"] = "5.5.2"

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    compileOnly "org.projectlombok:lombok"
    runtimeOnly "com.h2database:h2"
    annotationProcessor "org.projectlombok:lombok"

    testCompileOnly "net.bytebuddy:byte-buddy:1.10.13"  // allows mocking of classes (in addition to interfaces)
    testCompileOnly "cglib:cglib-nodep:3.3.0"           // used in spock
    testCompileOnly "org.objenesis:objenesis:3.1"
    // allows mocking of classes without default constructor (together with CGLIB)

    testImplementation "org.spockframework:spock-core:2.0-M3-groovy-3.0"
    testImplementation "org.spockframework:spock-spring:2.0-M3-groovy-3.0"
    testImplementation "org.spockframework:spock-junit4:2.0-M3-groovy-3.0"    // used for @Rule support
    testImplementation("org.codehaus.groovy:groovy-all:3.0.5") {
        exclude group: "org.codehaus.groovy", module: "groovy-templates"
        exclude group: "org.codehaus.groovy", module: "groovy-test"
        exclude group: "org.codehaus.groovy", module: "groovy-testng"
    }
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'com.tngtech.archunit:archunit:0.14.1'
}

test {
    useJUnitPlatform()
}
